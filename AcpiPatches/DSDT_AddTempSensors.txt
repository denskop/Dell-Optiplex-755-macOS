# Add Host Bus registers
into device name_adr Zero parent_hid PNP0A03 insert
begin
    OperationRegion (MCHB, PCI_Config, 0x48, 0x4)\n
    Field (MCHB, DWordAcc, NoLock, Preserve)\n
    {\n
        MHBR, 32,       // MCHBAR [31:15]\n
    }\n

    OperationRegion (DEVE, PCI_Config, 0x54, 0x1)\n
    Field (DEVE, ByteAcc, NoLock, Preserve)\n
    {\n
        D0EN, 1,        // DEV0 Enable\n
    }\n
end;

# Add MCH Temp Sensor
into device name_adr Zero parent_hid PNP0A03 insert
begin
    Device (MCHS)\n
    {\n
        Name (_HID, EisaId ("PNP0C02"))\n
        Name (_CID, "MON0000")\n

        OperationRegion(THSR, SystemMemory, (^MHBR & ~((1 << 15) - 1)) + 0xCD8, 8)\n
        Field(THSR, DWordAcc, NoLock, Preserve)\n
        {\n
            ,       7,\n
            TSE1,   1,          // Thermal Sensor Enable\n
            TE1,    1,          // Thermal Mode Enable and Rate\n
            Offset (0x03),\n
            TMP1,   8,          // Temp\n
            Offset (0x06),\n
            HTPS,   8           // Hot Trip Point Settings\n
        }\n

        Name (KLVN, Zero)   // Use Celsius\n

        Name (TEMP, Package (0x04)\n
        {\n
            "Mainboard",\n
            "TSYS"\n
        })\n

        Method (_INI, 0, NotSerialized)\n
        {\n
            MHBR |= 0x01        // Enable MCH Bar\n
            TSE1 = 0x01\n
            TE1  = 0x01\n
            HTPS = 0x00\n
        }\n

        Method (TSYS, 0, Serialized)\n
        {\n
            Local0 = TMP1\n
            // (0.0016 * TMP1^2) - (1.10707 * TMP1) + 161,05\n
            Return ((160 * Local0 * Local0 - 110707 * Local0 + 16105000) / 100000)\n
        }\n
    }\n
end;

# Reactivate MCH Temp Sensor after sleep
into method label XWAK insert
begin
    \_SB.PCI0.ESI.MCHS._INI()\n
end;
